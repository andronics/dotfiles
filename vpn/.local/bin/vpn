#!/usr/bin/env zsh

name=$(basename $0)

# ------------------------------

# https://www.reddit.com/r/nordvpn/comments/1b918b0/they_deprecated_the_server_api/
# https://api.nordvpn.com/v1/servers/recommendations?filters[country_id]=15

# ------------------------------

source "$(which _jq)"
source "$(which _log)"
source "$(which _rofi)"
source "$(which _string)"

# ------------------------------

vpn_cache_dir="${XDG_CACHE_HOME}/${name}"
vpn_config_dir="${XDG_CONFIG_HOME}/${name}"
vpn_data_dir="${XDG_DATA_HOME}/${name}"

# ------------------------------

vpn_servers=$(cat "${vpn_cache_dir}/servers.json")
vpn_states=$(cat "${vpn_data_dir}/states.json")

# ------------------------------

vpn_host="api.nordvpn.com"

# ------------------------------

source "$(which _args)"

# ------------------------------

_vpn() {

    [[ $# -eq 0 ]] && {
        log-error "command not specified"
        return 1
    }

    _command=$1
    shift 2>/dev/null

    case "${_command}" in

        list-cities) _vpn_list_cities "$@" ;;
        list-countries) _vpn_list_countries "$@" ;;
        list-groups) _vpn_list_groups "$@" ;;
        list-hostnames) _vpn_list_hostnames "$@" ;;
        list-services) _vpn_list_services "$@" ;;
        list-technologies) _vpn_list_technologies "$@" ;;
        
        meta) _vpn_meta "$@" ;;
        meta-bytes-received) _vpn_meta_bytes_received "$@" ;;
        meta-bytes-sent) _vpn_meta_bytes_sent "$@" ;;
        meta-city) _vpn_meta_city "$@" ;;
        meta-country) _vpn_meta_country "$@" ;;
        meta-ip) _vpn_meta_ip "$@" ;;
        meta-protocol) _vpn_meta_protocol "$@" ;;
        meta-server) _vpn_meta_server "$@" ;;
        meta-status) _vpn_meta_status "$@" ;;
        meta-status-icon) _vpn_meta_status_icon "$@" ;;
        meta-technology) _vpn_meta_technology "$@" ;;
        meta-uptime) _vpn_meta_uptime "$@" ;;

        state-connect) _vpn_state_connect "$@" ;;
        state-disconnect) _vpn_state_disconnect "$@" ;;
        state-name) _vpn_state_name "$@" ;;
        state-icon) _vpn_state_icon "$@" ;;
        
        *) log-warning "command '%s' unsupported" "${_command}" ;;

    esac

}

# ------------------------------

_vpn_list_cities() {
    _filter='.[].locations[0].country.city.name'
    echo "${vpn_servers}" | jq-query "${_filter}" | sort | uniq
}

_vpn_list_countries() {
    
    _filter='.[].locations[0].country.name'
    echo "${vpn_servers}" | jq-query "${_filter}" | sort | uniq

}

_vpn_list_hostnames() {
    
    _filter='.[].hostname'
    echo "${vpn_servers}" | jq-query "${_filter}" | sort | uniq

}

_vpn_list_groups() {
    
    _filter='.[].groups[].title'
    echo "${vpn_servers}" | jq-query "${_filter}" | sort | uniq

}

_vpn_list_services() {
    
    _filter='.[].services[].name'
    echo "${vpn_servers}" | jq-query "${_filter}" | sort | uniq

}

_vpn_list_technologies() {
    
    _filter='.[].technologies[].name'
    echo "${vpn_servers}" | jq-query "${_filter}" | sort | uniq

}

# ------------------------------

_vpn_meta() {
    # TODO: return extire json object
    echo $(nordvpn status | grep "$1" | cut -d ':' -f2)
}

_vpn_meta_bytes_received() {
    _vpn_meta "Transfer" | awk '{ print $1 " " $2 }' | string-lowercase
}

_vpn_meta_bytes_sent() {
    _vpn_meta "Transfer" | awk '{ print $4 " " $5 }' | string-lowercase
}

_vpn_meta_city() {
    _vpn_meta "City" | string-lowercase
}

_vpn_meta_country() {
    _vpn_meta "Country" | string-lowercase
}

_vpn_meta_ip() {
    _vpn_meta "IP" | string-lowercase
}

_vpn_meta_protocol() {
    _vpn_meta "Current protocol" | string-lowercase
}

_vpn_meta_server() {
    _vpn_meta "Server" | string-lowercase
}

_vpn_meta_status() {
    _vpn_meta "Status" | string-lowercase
}

_vpn_meta_status_icon() {
    
    _status=$(_vpn_meta_status | string-lowercase)
    _filter='.[] | select( .name | contains( $name ) ) | .icon'
    
    echo "${vpn_states}" | jq-query "${_filter}" "name" "${_status}"

}

_vpn_meta_technology() {
    echo $( _vpn_meta "Current technology" )
}

_vpn_meta_uptime() {
    echo $( _vpn_meta "Uptime" )
}

# ------------------------------

_vpn_server_update() {
    curl --silent "https://${vpn_host}/v1/servers" 
}

# ------------------------------

_vpn_state_connect() {

    _area=${1:-"city"}
    _locations=()

    case "${_area}" in
        city) _locations=$(_vpn_list_cities) ;;
        countries) _locations=$(_vpn_list_countries)  ;;
        technologies) _locations=$(_vpn_list_technologies) ;;
    esac

    _location=$(printf "%s\n" "${_locations[@]}" | rofi-dmenu "Location $(echo "${_area}" | string-capitalcase )" )

    [[ -n ${_location} ]] || {
        log-info "failed to specify location"
        return 1
    } 

    _vpn_state_disconnect

    nordvpn connect "${_location}"
}

_vpn_state_disconnect() {
    _status=$(_vpn_meta_status)
    [[ ${_status} != "disconnected" ]] && {
        log-info "diconnecting from service"
        nordvpn disconnect > /dev/null
    }
}

# ------------------------------

_vpn "$@"